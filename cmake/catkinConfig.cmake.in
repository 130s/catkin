# generated from catkin/cmake/catkin-config.cmake.in
# which overlays the default template catkin/cmake/template/pkg-config.cmake.in
#
# :outvar catkin_INCLUDE_DIRS: contains the include dirs of all searched components.
#    For use with CMake ``include_directories(${catkin_INCLUDE_DIRS})``.
# :outvar catkin_LIBRARY_DIRS: contains the library dirs of all searched components.
#    For use with CMake ``link_directories(${catkin_INCLUDE_DIRS})``.
# :outvar catkin_LIBRARIES: contains the include dirs of all searched components.
#    For use with CMake ``include_directories(${catkin_INCLUDE_DIRS})``.
# :outvar <comp>_INCLUDE_DIRS/_LIBRARY_DIRS/_LIBRARY:
#    contains the include dirs / library dirs / libraries of the searched component <comp>.

# prevent usage with wrong case
if(CATKIN_FIND_COMPONENTS OR CATKIN_FIND_REQUIRED OR CATKIN_FIND_QUIETLY)
  message(FATAL_ERROR "find_package() only supports lower-case package name 'catkin'")
endif()

# mark as found
if(NOT catkin_FOUND)
  set(catkin_FOUND)
  set(CATKIN_PACKAGE_PREFIX "" CACHE STRING "Prefix to apply to package generated via gendebian")
endif()

# list of unique workspaces based on CATKIN_WORKSPACES and CMAKE_PREFIX_PATH
# considers CMAKE_PREFIX_PATH for the case that setup.sh has not been sourced
# the same list is built in cmake/toplevel.cmake
if(NOT DEFINED CATKIN_WORKSPACES)
  set(CATKIN_WORKSPACES "")
  foreach(workspace $ENV{CATKIN_WORKSPACES})
    string(REGEX REPLACE ":.*" "" workspace ${workspace})
    list(FIND CATKIN_WORKSPACES ${workspace} _index)
    if(_index EQUAL -1)
      list(APPEND CATKIN_WORKSPACES ${workspace})
    endif()
  endforeach()
  foreach(path ${CMAKE_PREFIX_PATH})
    if(EXISTS ${path}/CATKIN_WORKSPACE)
      list(FIND CATKIN_WORKSPACES ${path} _index)
      if(_index EQUAL -1)
        list(APPEND CATKIN_WORKSPACES ${path})
      endif()
    endif()
  endforeach()
endif()

# prepend all workspaces to the CMAKE_PREFIX_PATH
# for the case that setup.sh has not been sourced
set(workspaces "")
foreach(workspace $ENV{CATKIN_WORKSPACES})
  string(REGEX REPLACE ":.*" "" workspace ${workspace})
  list(INSERT workspaces 0 ${workspace})
endforeach() 
foreach(workspace ${workspaces})
  list(FIND CMAKE_PREFIX_PATH ${workspace} _index)
  if(_index EQUAL -1)
    list(INSERT CMAKE_PREFIX_PATH 0 ${workspace})
  endif()
endforeach()

# the following lines must be the same as in catkin/CMakeLists.txt
set(catkin_BUILD_PREFIX ${CMAKE_BINARY_DIR}/buildspace CACHE PATH "catkin buildspace")
# compare boolean as string since policy CMP0012 is only enabled afterwards
if("@PKG_BUILDSPACE@" STREQUAL "TRUE")
  set(catkin_EXTRAS_DIR @catkin_EXTRAS_DIR@)
else()
  set(catkin_EXTRAS_DIR @PKG_CMAKE_DIR@)
endif()
# this line can not be moved to the toplevel.cmake since buildspace must be on prefix path even if toplevel.cmake is not used
list(INSERT CMAKE_PREFIX_PATH 0 ${CMAKE_BINARY_DIR}/buildspace)

include(${catkin_EXTRAS_DIR}/all.cmake)

# don't overwrite catkin_* variables when being called recursively
if(NOT _CATKIN_FIND_ OR _CATKIN_FIND_ EQUAL 0)
  set(_CATKIN_FIND_ 0)
  set(catkin_INCLUDE_DIRS "")
  set(catkin_LIBRARIES "")
  set(catkin_LIBRARY_DIRS "")
endif()

# increment recursion counter
math(EXPR _CATKIN_FIND_ "${_CATKIN_FIND_} + 1")

# find all components
foreach(component ${catkin_FIND_COMPONENTS})
  string(TOLOWER "${component}" component_lower)
  if(${component_lower} STREQUAL "catkin")
    message(FATAL_ERROR "'${component}' is not a valid component for find_package()")
  endif()

  # get search paths from all CMAKE_PREFIX_PATH (which includes buildspace and all workspaces)
  set(paths "")
  foreach(path ${CMAKE_PREFIX_PATH})
    if(IS_DIRECTORY ${path}/share/${component}/cmake)
      list(APPEND paths ${path}/share/${component}/cmake)
    endif()
  endforeach()

  # find package component
  if(catkin_FIND_REQUIRED)
    find_package(${component} REQUIRED NO_MODULE PATHS ${paths}
      NO_DEFAULT_PATH NO_CMAKE_FIND_ROOT_PATH)
  elseif(catkin_FIND_QUIETLY)
    find_package(${component} QUIET NO_MODULE PATHS ${paths}
      NO_DEFAULT_PATH NO_CMAKE_FIND_ROOT_PATH)
  else()
    find_package(${component} NO_MODULE PATHS ${paths}
      NO_DEFAULT_PATH NO_CMAKE_FIND_ROOT_PATH)
  endif()

  # append component-specific variables to catkin_* variables
  list_append_unique(catkin_INCLUDE_DIRS ${${component}_INCLUDE_DIRS})
  list_append_unique(catkin_LIBRARIES ${${component}_LIBRARIES})
  list_append_unique(catkin_LIBRARY_DIRS ${${component}_LIBRARY_DIRS})
endforeach()

# decrement recursion counter
math(EXPR _CATKIN_FIND_ "${_CATKIN_FIND_} - 1")
